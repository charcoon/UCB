DIFF:EMA(CLOSE,12)-EMA(CLOSE,26);
DEA:EMA(DIFF,9);
MACD:(DIFF-DEA)*2,COLORSTICK;
N1:=BARSLAST(CROSS(DEA,DIFF));
N2:=REF(BARSLAST(CROSS(DEA,DIFF)),N1+1);
M1:=BARSLAST(CROSS(DIFF,DEA));
M2:=REF(BARSLAST(CROSS(DIFF,DEA)),M1+1);
CL1:=LLV(CLOSE,N1+1),NODRAW;
CL2:=REF(CL1,M1+1),NODRAW;
CL3:=REF(CL2,M1+1),NODRAW;
DIFL1:=LLV(DIFF,N1+1),NODRAW;
DIFL2:=REF(DIFL1,M1+1),NODRAW;
DIFL3:=REF(DIFL2,M1+1),NODRAW;
直接底钝化:=CL1<CL2 AND DIFL1>DIFL2 AND REF(MACD,1)<0 AND DIFL2<0,NODRAW;
隔峰底钝化:=CL1<CL3 AND DIFL1<DIFL2 AND DIFL1>DIFL3 AND DIFF<DEA AND REF(MACD,1)<0 AND DIFL3<0,NODRAW;
底部钝化:=(直接底钝化 OR 隔峰底钝化) AND REF(MACD,1)<0,NODRAW;
底钝化:=底部钝化 AND REF(底部钝化,1)=0 AND DIFF<DEA,NODRAW;
底钝化消失:=(REF(直接底钝化,1) AND DIFL1<DIFL2 AND DIFF<DEA) OR (REF(隔峰底钝化,1) AND DIFL1<DIFL3 AND DIFF<DEA),NODRAW;
底部结构:=DIFF>REF(DIFF,1) AND REF(底部钝化,1) AND DIFL1*0.9884<DIFF,NODRAW;
底结构形成:=底部结构 AND REF(底部结构,1)=0,NODRAW;
底再次钝化1:=COUNT(底部结构,5) AND 底部钝化 AND DIFF<REF(DIFF,1)*1.01,NODRAW;
底再次钝化:=底再次钝化1 AND REF(底再次钝化1,1)=0,NODRAW;
M3:=BARSLAST(底结构形成)-1,NODRAW;
M4:=BARSLAST(底钝化 OR 底再次钝化),NODRAW;
M5:=BARSLAST(CROSS(DIFF,0)),NODRAW;
{分析家公式网 WWW.70822.COM 整理}
M6:=C<REF(CL1,M3) AND C<REF(CL2,M3) AND M3<M4 AND M3<M5,NODRAW;
底结构消失1:=COUNT(M6,M3)=1 AND REF(M6,1)=0 AND M3<M4 AND M3<M5,NODRAW;
底结构消失:=底结构消失1 AND REF(底结构消失1,1)=0 AND REF(底结构消失1,2)=0,NODRAW;
DRAWTEXT(底结构形成,DIFF/0.88,'结构形成'),COLORMAGENTA;
DRAWTEXT(底钝化 OR 底再次钝化,DIFF/0.80,'钝化'),COLORYELLOW;
DRAWTEXT((底结构消失 OR 底钝化消失) AND 底部钝化=0 AND 底再次钝化=0,DIFF/0.75,'消失'),COLORGREEN;
CH1:=HHV(CLOSE,M1+1),NODRAW;
CH2:=REF(CH1,N1+1),NODRAW;
CH3:=REF(CH2,N1+1),NODRAW;
DIFH1:=HHV(DIFF,M1+1),NODRAW;
DIFH2:=REF(DIFH1,N1+1),NODRAW;
DIFH3:=REF(DIFH2,N1+1),NODRAW;
直接顶钝化:=CH1>CH2 AND DIFH1<DIFH2 AND REF(MACD,1)>0 AND DIFH2>0,NODRAW;
隔峰顶钝化:=CH1>CH3 AND DIFH1>DIFH2 AND DIFH1<DIFH3 AND REF(MACD,1)>0 AND DIFH3>0,NODRAW;
顶部钝化:=(直接顶钝化 OR 隔峰顶钝化) AND REF(MACD,1)>0,NODRAW;
顶钝化:=顶部钝化 AND REF(顶部钝化,1)=0 AND DIFF>DEA,NODRAW;
顶钝化消失:=(REF(直接顶钝化,1) AND DIFH1>=DIFH2 AND DIFF>DEA) OR (REF(隔峰顶钝化,1) AND DIFH1>=DIFH3 AND DIFF>DEA),NODRAW;{分析家公式网 WWW.70822.COM 整理}
顶部结构:=REF(顶部钝化,1) AND DIFF<REF(DIFF,1) AND DIFH1*0.99>DIFF,NODRAW;
顶结构形成:=顶部结构 AND REF(顶部结构,1)=0,NODRAW;
顶再次钝化1:=COUNT(顶部结构,5) AND 顶部钝化 AND DIFF*0.99>REF(DIFF,1),NODRAW;
顶再次钝化:=顶再次钝化1 AND REF(顶再次钝化1,1)=0,NODRAW;
N3:=BARSLAST(顶结构形成)-1,NODRAW;
N4:=BARSLAST(顶钝化 OR 顶再次钝化),NODRAW;
N5:=BARSLAST(CROSS(0,DIFF)),NODRAW;
N6:=C<REF(CH1,N3) AND C<REF(CH2,N3) AND N3<N4 AND N3<N5,NODRAW;
顶结构消失1:=COUNT(N6,N3)=1 AND REF(N6,1)=0 AND N3<N4 AND N3<N5,NODRAW;
顶结构消失:=顶结构消失1 AND REF(顶结构消失1,1)=0 AND REF(顶结构消失1,2)=0,NODRAW;
DRAWTEXT(顶结构形成,DIFF*1.15,'结构形成'),COLORMAGENTA;
DRAWTEXT(顶钝化 OR 顶再次钝化,DIFF*1.20,'钝化'),COLORYELLOW;
DRAWTEXT((顶结构消失 OR 顶钝化消失) AND 顶部钝化=0 AND 顶再次钝化=0,DIFF*1.25,'消失'),COLORGREEN;

定量结构

VAR1:=LLV(LOW,13);
VAR2:=HHV(HIGH,13);
VAR3:=SMA((CLOSE-VAR1)/(VAR2-VAR1)*100,5,1);
VAR4:=SMA((VAR2-CLOSE)/(VAR2-VAR1)*100,5,1);
DRAWBAND(VAR3,RGB(155,50,50),50,RGB(0,100,50));
主力:VAR3,COLORRED;
散户:VAR4,COLORGREEN;
VAR5:=SMA(MAX(CLOSE-REF(CLOSE,1),0),5,1)/SMA(ABS(CLOSE-REF(CLOSE,1)),5,1)*100;
操作:EMA(VAR5,3),COLORYELLOW;
STICKLINE(C,90,90,1,0),COLOR3C3CFF;
STICKLINE(C,50,50,1,0),COLORYELLOW;
STICKLINE(C,10,10,1,0),COLORCYAN;


一根线

个股资金线:MA(C,1)/MA(REF(C,18),18)*100,LINETHICK2,COLOR00FFFF;
资金生命线:MA(FORCAST(个股资金线,20),6),COLORMAGENTA,LINETHICK2;

资金线

E:=(H+L+O+2*C)/5;
次日突破价:=E+(H-L);
突:=REF(次日突破价,1),NODRAW;
次日阻力价:=2*E-L;
阻:=REF(次日阻力价,1),NODRAW;
次日支撑价:=2*E-H;
支:=REF(次日支撑价,1),NODRAW;
次日下跌反转价:=E-(H-L);
转:=REF(次日下跌反转价,1),NODRAW;
保:=(REF(C,1)+REF(C,2)+O)/3,COLORCYAN,NODRAW;
BAO1:=保;{分析家公式网 WWW.70822.COM 整理}
MA5:MA(CLOSE,5),COLORRED;
判5:IF(MA5<REF(MA5,1),MA5,DRAWNULL),COLORWHITE;
MA13:MA(CLOSE,13),COLORGREEN;
判13:IF(MA13<REF(MA13,1),MA13,DRAWNULL),COLORBLUE;
MA20:MA(CLOSE,20),COLORMAGENTA,LINETHICK2;
判20:IF(MA20<REF(MA20,1),MA20,DRAWNULL),COLORWHITE,LINETHICK2;
MA60:=MA(CLOSE,60),COLORYELLOW,LINETHICK3;
判60:=IF(MA60<REF(MA60,1),MA60,DRAWNULL),COLORWHITE,LINETHICK3;
MA120:=MA(CLOSE,120),COLORBLUE,LINETHICK3;
判120:=IF(MA120<REF(MA120,1),MA120,DRAWNULL),COLORWHITE,LINETHICK3;
MA200:=MA(CLOSE,200),COLORGRAY;
判200:=IF(MA200<REF(MA200,1),MA200,DRAWNULL),COLORWHITE;
获利20:C/MA20,COLORMAGENTA,NODRAW;
获利60:=C/MA60,COLORYELLOW,NODRAW;
DD:=C; 即时点:REFDATE(DD,DATE),DOTLINE,COLORWHITE,LINETHICK1;
C22:=REF(C,1);
涨停1:=IF((C/C22*100-100>=9.86),1,0);
STICKLINE(涨停1,OPEN,CLOSE,2,0),,COLORYELLOW;
STICKLINE(涨停1,H,L,0,0),,COLORYELLOW;
C23:=REF(C,1);
大阳:=IF((C/C23*100-100>=7),0.6,0) AND IF((C/C23*100-100<9.86),1,0) ;
STICKLINE(大阳,OPEN,CLOSE,0.6,0),,COLORMAGENTA;
STICKLINE(大阳,H,L,0,0),,COLORRED;
大阳1:=IF((C/C23*100-100>=4),0.6,0) AND IF((C/C23*100-100<7),1,0) ;
STICKLINE(大阳1,OPEN,CLOSE,0.6,0),,COLORRED;
STICKLINE(大阳1,H,L,0,0),,COLORRED;
C24:=(C-C23)/3;
XX:=C-C24*2;
实顶下浮:XX,COLORYELLOW,NODRAW;
{跳空}
STICKLINE(REF(L,1)>H,H,REF(L,1),5,0),COLORGRAY;
STICKLINE(REF(H,1)<L,L,REF(H,1),5,0),COLORGRAY;
LTB:=FINANCE(7)*100/FINANCE(1);
ZGB:=FINANCE(1)/100000000;
JJS:=LTB-REF(LTB,1);
DRAWTEXT(LTB<REF(LTB,1)*0.95 AND ZGB>REF(ZGB,1),L*0.990,'定向增发'),COLORRED;
DRAWTEXT(ZGB<REF(ZGB,1)*0.95 AND LTB>REF(LTB,1),L*0.990,'回购'),COLORGREEN;
DRAWTEXT(LTB>REF(LTB,1)*1.05 AND ZGB=REF(ZGB,1),L*0.990,'大小非'),COLORRED;
DRAWTEXT(LTB>REF(LTB,1)*1.05 AND ZGB>REF(ZGB,1),L*0.990,'公开发行');
TJ:=ISLASTBAR AND C<500;
DRAWTEXT_FIX(TJ,0,0,0,HYBLOCK);{在屏幕左上角显示所属行业板块}
DRAWTEXT_FIX(TJ,0.05,0,0,DYBLOCK);{在屏幕左上角显示所属地域板块}
DRAWTEXT_FIX(TJ,0.1,0,0,GNBLOCK);{在屏幕左上角显示所属概念板块}
DRAWTEXT_FIX(TJ,0.25,0.035,0,FGBLOCK),COLORGREEN;
单阳:C*C/REF(C,1)/0.9,NODRAW,COLOR0000FF;
单阴:C*C/REF(C,1)/1.1,NODRAW;

主图